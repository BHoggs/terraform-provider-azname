---
page_title: "Data Source azname_name"
subcategory: ""
description: |-
  Data source for generating standardized Azure resource names following naming conventions.
---

# Data Source: azname_name

Data source for generating standardized Azure resource names following naming conventions.

This data source uses templates to generate consistent resource names based on inputs like workload name,
environment, resource type, and location. It supports both global resources and child resources.

Data sources do not persist generated names in Terraform state. As a result, any changes to naming conventions or input parameters will lead to new names being generated on each run. This can cause issues with resource recreation in Azure, as resource names are immutable identifiers.

~> **NOTE:** As data sources are not persisted in state, it's recommended to always set the `random_seed` attribute to ensure consistent random suffixes across runs.

## Example Usage

```terraform
terraform {
  required_providers {
    azname = {
      source = "BHoggs/azname"
    }
  }
}

# Provider configuration with global settings
# These settings (separator, random_length, clean_output, environment, etc.) apply to all data sources
provider "azname" {
  environment   = "prod"
  random_length = 4
  separator     = "-"
  clean_output  = true
}

# Basic resource name generation using data source
# Data sources are useful for generating names without managing state
# Required attributes: name, resource_type
# Environment is inherited from provider configuration
data "azname_name" "resource_group" {
  name          = "myapp"
  resource_type = "azurerm_resource_group"
  location      = "westus2" # Resource-specific attribute
}

# Output: rg-myapp-prod-westus2-1234

# Using a custom name to override generation
# Useful when you need to reference existing resources with specific names
data "azname_name" "legacy_storage" {
  name          = "myapp"
  resource_type = "azurerm_storage_account"
  custom_name   = "legacystorage123"
}

# Output: legacystorage123

# Child resource using parent name
# Demonstrates hierarchical naming for related resources
data "azname_name" "container" {
  name          = "images"
  resource_type = "azurerm_storage_container"
  parent_name   = data.azname_name.legacy_storage.result
  service       = "blob" # Resource-specific attribute
}

# Output: legacystorage123-container-images-1234
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name` (String) The workload or application name to use in the resource name.
- `resource_type` (String) The Azure resource type abbreviation (e.g., rg for resource group, kv for key vault).

### Optional

- `custom_name` (String) Override the generated name with a custom value. Useful for legacy or imported resources.
- `environment` (String) The environment name (e.g., dev, test, prod) to use in the resource name. Defaults to provider-level environment if not set.
- `instance` (Number) Instance number for the resource. Used when deploying multiple instances of the same resource type.
- `location` (String) Azure region where the resource will be deployed. Will be included in the name if specified in the template.
- `parent_name` (String) Name of the parent resource. Required when generating names for child resources.
- `prefixes` (List of String) List of prefixes to prepend to the resource name. These will be joined using the separator character.
- `random_seed` (Number) Seed value for random suffix generation. Use this to get consistent random values.
- `separator` (String) Character to use as separator in the resource name. Must be a single character. Defaults to provider's separator setting.
- `service` (String) Service or component identifier within the workload (e.g., web, api, worker).
- `suffixes` (List of String) List of suffixes to append to the resource name. These will be joined using the separator character.

### Read-Only

- `id` (String) ID of the data source, same as result.
- `result` (String) The generated resource name following the configured template pattern.
