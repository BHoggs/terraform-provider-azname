---
page_title: "Resource azname_name"
subcategory: ""
description: |-
  Resource for generating standardized Azure resource names following naming conventions.
---

# Resource: azname_name

Resource for generating standardized Azure resource names following naming conventions.

This resource uses templates to generate consistent resource names based on inputs like workload name,
environment, resource type, and location. It supports both global resources and child resources.

The `azname_name` resource persists generated names in Terraform state. This means that once a name is generated, it remains stable even if naming conventions or input parameters change. This is crucial for Azure resources, as their names are immutable identifiers. By storing names in state, this resource helps prevent unintended resource recreation and the associated downtime and data loss.

## Example Usage

```terraform
terraform {
  required_providers {
    azname = {
      source = "BHoggs/azname"
    }
  }
}

# Provider-level configuration sets defaults that apply to all resources
# These settings (separator, random_length, prefixes, suffixes, environment) can be
# configured here to avoid repeating them in each resource
provider "azname" {
  environment   = "prod"
  random_length = 4
  separator     = "-"
}

# Basic resource name generation
# Required: name, resource_type
# Resource-specific attributes like location and service should be set here
resource "azname_name" "web_app" {
  name          = "myapp"
  resource_type = "azurerm_linux_web_app"
  location      = "westus2" # Resource-specific, should be set at resource level
  service       = "web"     # Resource-specific, should be set at resource level

  # Name will be regenerated if any of these triggers change
  # Useful for forcing new names when deployment parameters change
  triggers = {
    key = "value"
  }
}

# Child resource that depends on parent
# Uses parent_name to create hierarchical naming
resource "azname_name" "deployment_slot" {
  name          = "blue"
  resource_type = "azurerm_function_app_slot"
  parent_name   = azname_name.web_app.result

  # Trigger regeneration when parent name changes
  triggers = {
    parent_name = azname_name.web_app.result
  }
}

# Using instance numbers for multiple similar resources
# The instance number will be formatted according to provider's instance_length setting
resource "azname_name" "vm" {
  name          = "web"
  resource_type = "azurerm_virtual_machine"
  location      = "westus2"
  instance      = 1
}

# Using custom name for imported legacy resource
# When custom_name is set, it overrides the generated name entirely
resource "azname_name" "legacy_db" {
  name          = "myapp"
  resource_type = "azurerm_mssql_database"
  custom_name   = "legacy-sql-db-01"
}

# Example showing resource-level override of provider settings
# You can override environment, separator, or other provider settings per-resource if needed
resource "azname_name" "special_storage" {
  name          = "data"
  environment   = "dev" # Override provider environment for this specific resource
  resource_type = "azurerm_storage_account"
  location      = "eastus"
  separator     = "" # Override provider separator for this specific resource

  # Resource-level prefixes/suffixes are also supported
  prefixes = ["corp"]
  suffixes = ["primary"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `environment` (String) The environment name (e.g., dev, test, prod) to use in the resource name.
- `name` (String) The workload or application name to use in the resource name.
- `resource_type` (String) The Azure resource type abbreviation (e.g., rg for resource group, kv for key vault).

### Optional

- `custom_name` (String) Override the generated name with a custom value. Useful for legacy or imported resources.
- `instance` (Number) Instance number for the resource. Used when deploying multiple instances of the same resource type.
- `location` (String) Azure region where the resource will be deployed. Will be included in the name if specified in the template.
- `parent_name` (String) Name of the parent resource. Required when generating names for child resources.
- `prefixes` (List of String) List of prefixes to prepend to the resource name. These will be joined using the separator character.
- `random_seed` (Number) Seed value for random suffix generation. Use this to get consistent random values.
- `separator` (String) Character to use as separator in the resource name. Must be a single character. Defaults to provider's separator setting.
- `service` (String) Service or component identifier within the workload (e.g., web, api, worker).
- `suffixes` (List of String) List of suffixes to append to the resource name. These will be joined using the separator character.
- `triggers` (Map of String) Map of values that should trigger a new name to be generated when changed. Common triggers include version numbers or Git commit hashes.

### Read-Only

- `result` (String) The generated resource name following the configured template pattern.

## Import

Import is supported using the following syntax:

```shell
# terraform import azname_name.example <id>
terraform import azname_name.example "rg-myapp-prod-eastus2"
```
